<html>
<style>
* {
   margin: 0;
   padding: 0;
}
body, html {height:100%;}
div.wrap {height:100%; overflow:hidden;}
body {overflow: hidden; }
</style>
<script src="../../lib/colormaps.js"></script>
<script src="../../lib/gpgpu.js"></script>
<script src="../../lib/gpugaussian.js"></script>
<script src="../../lib/gpurand.js"></script>

<script id="retinalmap" type="x-shader/x-fragment">
uniform sampler2D data;
uniform sampler2D noise;
uniform float t;
#define twopi   6.283185307179586
#define over2pi 0.159154943091895
float max3 (vec3 v) {
  return max (max (v.x, v.y), v.z);
}
void main() {
    vec2 XY = gl_FragCoord.xy/vec2(W,H);

    // noisemaker
    vec4 n  = texture2D(noise, XY)*.999+0.0001; // uniform noise
    vec2 R  = sqrt(-2.*log(n.xy));  // Transform uniform noise
    vec2 T  = twopi*n.zw;           // into Gaussian using Box-
    vec4 N  = vec4(R*cos(T),R*sin(T))*.1; // Muller transform.


    // remapper
    vec2 z = vec2(XY);
    z = z*2.-1.;
    vec2 u;
    u.x = atan(z.y,z.x)-t*0.0003;
    u.y = log(length(z))-t*0.001;

    vec4 RGBA = texture2D(data,u*over2pi);

    // huerotator
    float th = -t*0.005;
    float Q1 = sin(th)/sqrt(3.);
    float Q2 = (1.-cos(th))/3.;
    float rb = RGBA.r-RGBA.b;
    float gr = RGBA.g-RGBA.r;
    float bg = RGBA.b-RGBA.g;
    float r1 = Q2*(gr-rb)-Q1*bg+RGBA.r;
    float Z  = Q2*(bg-rb)+Q1*gr;
    vec4 color = vec4(r1,RGBA.g+Z+(RGBA.r-r1),RGBA.b-Z,RGBA.a);
    //color *= max3(RGBA.rgb)/max3(color.rgb);
    color *= length(RGBA)/length(color);
    gl_FragColor = color+N;
}
</script>

<script id="wc" type="x-shader/x-fragment">
#define dt (float(1))
#define Te (float(4))
#define Ti (float(12.5))
#define ai (float(100))
#define ae (float(50))
#define Aee (float(16.1))
#define Aie (float(14.5))
#define Aei (float(13.7))
#define Aii (float(13))
#define He (float(3.1))
#define Hi (float(3.5))
#define Ne (float(0.75))
#define Ni (float(0))
#define bi (float(0))
#define be (float(0))
uniform sampler2D Uin;
uniform sampler2D Uconv;
uniform sampler2D noise;
uniform float s;
#define twopi 6.283185307179586
#define F(x) (1./(1.+exp(-(x))))
void main() {
    vec2 XY   = gl_FragCoord.xy/vec2(W,H);
    vec4 U    = texture2D(Uin  , XY); // raw fields
    vec4 Uc   = texture2D(Uconv, XY); // convolved fields

    vec4 n    = texture2D(noise, XY)*.999+0.0001; // uniform noise
    vec2 R    = sqrt(-2.*log(n.xy));  // Transform uniform noise
    vec2 T    = twopi*n.zw;           // into Gaussian using Box-
    vec4 N    = vec4(R*cos(T),R*sin(T))*sqrt(dt); // Muller transform.

    float Ae  = F(Aee*Uc.x-Aie*Uc.y-He-U.z*be+Ne*N.x);
    float Ai  = F(Aei*Uc.x-Aii*Uc.y-Hi-U.w*bi+Ne*N.x);
    float Ue  = U.x + (Ae -U.x)*(dt/Te);
    float Ui  = U.y + (Ai -U.y)*(dt/Ti);
    float Ve  = U.z + (U.x-U.z)*(dt/ae);
    float Vi  = U.w + (U.y-U.w)*(dt/ai);
    gl_FragColor = floor(255.*vec4(Ue,Ui,Ve,Vi)+.5)/255.;
}
</script>
<script id="colormap" type="x-shader/x-fragment">
uniform sampler2D Uin;
void main() {
    vec2  XY = gl_FragCoord.xy/vec2(W,H);
    vec4  U  = texture2D(Uin   ,XY);
    float Ue  = U.r;
    float Ui  = U.g;
    gl_FragColor = vec4(Ue,Ui,abs(Ue-Ui)*3.0,1.);
}
</script>

<script>

function make_fullscreen()
{
    // Retrieve a handle to the canvas element
    var canvas = $("maincanvas");
    var wrap = $("wrapper");
    var w = document.body.clientWidth ;
    var h = document.body.clientHeight;
    var s = Math.max(w,h);
    canvas.style.width  = s;
    canvas.style.height = s;
    if (h<s) {
      canvas.style.marginTop = -(s-h)*0.5;
    }
    else if (w<s) {
      canvas.style.marginLeft = -(s-w)*0.5;
    }
}

// This is the main script that will run when the website loads
function main()
{
    // Retrieve a handle to the canvas element
    var canvas = $("maincanvas");
    // Create a WebGL context on the canvas, abort if fail
    var gl = getRasterGL(canvas);
    if (!gl) OUT;

    make_fullscreen();
    var wc     = getRasterProgram(gl,'wc',{});
    var temp   = newBasicFramebuffer(gl,{wrap : gl.REPEAT});
    var U      = newBasicFramebuffer(gl,{wrap : gl.REPEAT});
    var UC     = newBasicFramebuffer(gl,{wrap : gl.REPEAT});
    var noise  = newBasicFramebuffer(gl,{wrap : gl.REPEAT});
    var color  = getRasterProgram(gl,'colormap');
    var map    = getRasterProgram(gl,'retinalmap');
    var copy   = GPUcopy(gl);
    var blur   = GPUGaussianMultiBlur(gl,2,2.9,1,1);
    var gpurng = GPUNoise(gl);

    function randomize() {
        gpurng.randomize(U);
        gpurng.randomize(noise);
    }
    randomize();

    var t = 0;
    function animate() {
        setTimeout(function(){
            gpurng(noise,temp);
            blur(U,temp,UC);
            wc({Uin   :U,
                Uconv :UC,
                noise :noise,
                },temp);
            copy(temp,U);
            color({Uin:U},temp);
            map({
                noise : noise,
                data  : temp,
                t : t});
            t += 1;
            make_fullscreen();
            requestAnimationFrame(animate);
        },0);
    }
    animate();
}

</script>
<body onload="javascript:main()" onresize="javascript:make_fullscreen()">
<div class=wrap id='wrapper'>
<canvas id='maincanvas' style="width:1024px;height:1024px;"></canvas>
</div>
</body>
</html>



